plugins {
    id "eclipse"
    id "idea"
    id "org.sonarqube" version "2.6"
    id "net.ltgt.apt" version "0.17" apply false
}

ext {
    lombokVersion = "1.18.4"
    baseName = "zoauth"
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectKey", project.name
    }
}

allprojects {
    version = project.version + "-" + project.semanticVersion

    configurations {
        providedRuntime
    }

    repositories {
        mavenLocal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        mavenCentral()
    }
    
    apply plugin: "eclipse"
    apply plugin: "idea"
}

def static computeGroup(project) {
    if (project.parent == null) {
        return project.group
    }
    return computeGroup(project.parent) + "." + project.parent.name
}

def static computeBaseName(project) {
    if (project.parent == null) {
        return project.ext.baseName
    }
    return computeBaseName(project.parent) + "-" + project.name
}

subprojects {
    if (!project.name.startsWith("android")) {
        apply plugin: "java"
    }
    apply plugin: "java-library-distribution"
    apply plugin: "jacoco"
    apply plugin: "net.ltgt.apt"
    group = computeGroup(project)

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testImplementation "junit:junit:4.12"
        testImplementation "org.skyscreamer:jsonassert:1.2.3"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    }

    apply from: "$rootDir/buildSrc/javadoc.gradle"
    apply from: "$rootDir/buildSrc/bundle.gradle"
    apply from: "$rootDir/buildSrc/nexus.gradle"
    apply from: "$rootDir/buildSrc/misc.gradle"

}

apply from: "buildSrc/verification.gradle"
apply from: "buildSrc/dist.gradle"
